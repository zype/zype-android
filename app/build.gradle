buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        flatDir {
            dirs 'libs'
        }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.22.1'
    }
}
import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
// TODO: Uncomment following line to use Fabric
//apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
}

android {
    def String majorKey = 'MAJOR_VERSION'
    def String minorKey = 'MINOR_VERSION'
    def String versionCodeKey = 'DEBUG_VERSION_CODE'
    def versionPropertiesFile = file('version.properties')
    def Properties versionProperties = readVersionProperties(versionPropertiesFile)

    def df = new SimpleDateFormat("yyyy-MM-dd HH:mmZ")
    def String date = df.format(new Date(System.currentTimeMillis()))

    def getDate = { ->
        def dateFormatted = new Date()
        def formattedDate = dateFormatted.format('yyyyMMdd')
        return formattedDate
    }

    def currentCodeVersion = versionProperties[versionCodeKey].toString().toInteger()
    def zypeTemplateVersion = "1.10.3"

    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    dexOptions {
        jumboMode true
    }
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        multiDexEnabled true
        versionCode currentCodeVersion
        buildConfigField "String", "ZYPE_TEMPLATE_VERSION", "\"${zypeTemplateVersion}\""

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            setVersionNameSuffix(".${currentCodeVersion}")
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            def int newVersionCode = currentCodeVersion + 1
            versionProperties[versionCodeKey] = newVersionCode.toString()
            store(versionPropertiesFile, versionProperties)
        }
        debug {
            setVersionNameSuffix(".${currentCodeVersion} DEBUG [${date}]")
        }
    }

    flavorDimensions "main"
    productFlavors {
        zype {
            versionName zypeTemplateVersion
            applicationId 'com.zype.android.template'
            manifestPlaceholders = [manifestApplicationId          : "${applicationId}",
                                    onesignal_app_id               : "",
                                    onesignal_google_project_number: ""]
        }
        template {
            versionName '1.0.0'
            applicationId "<APPLICATION_ID>"
            manifestPlaceholders = [manifestApplicationId          : "${applicationId}",
                                    // TODO: Provide valid app_id and google_project_number for OneSignal
                                    onesignal_app_id               : "",
                                    onesignal_google_project_number: ""]
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputNameNew = "Zype-Android" + "-" + variant.productFlavors.name + "-" + variant.buildType.name +
                    "-" + variant.productFlavors.versionName + "-" + getDate() + ".apk"
            outputFileName = outputNameNew
        }
    }
}

public def readVersionProperties(File from) {
    if (from.canRead()) {
        def Properties result = new Properties()
        result.load(new FileInputStream(from))
        return result
    } else {
        throw new GradleException("Could not read version.properties!")
    }
}

public def store(File to, Properties properties) {
    properties.store(to.newWriter(), null)
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'com.android.support:multidex:1.0.3'

    implementation('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true;
    }
    androidTestImplementation 'com.jayway.android.robotium:robotium-solo:5.4.1'
    implementation project(':CastCompanionLibrary')
//    compile 'com.google.android.libraries.cast.companionlibrary:ccl:2.9.1'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:mediarouter-v7:27.1.1'
    implementation 'com.android.support:customtabs:27.1.1'

    implementation 'com.squareup.retrofit:retrofit:1.9.0'
    implementation 'com.squareup.okhttp:okhttp:2.6.0'
    implementation 'com.squareup:otto:1.3.8'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.ns-developer:tagcloudview:0.1.0'
    implementation 'com.google.android.exoplayer:exoplayer:r1.5.12'
    implementation 'com.onesignal:OneSignal:3.8.4@aar'

    implementation('com.github.ozodrukh:CircularReveal:2.0.1@aar') {
        transitive = true;
    }

    implementation 'com.google.android.gms:play-services-gcm:15.0.1'
    implementation 'com.google.android.gms:play-services-analytics:16.0.0'
    implementation 'com.google.android.gms:play-services-location:15.0.1'
    implementation 'com.google.android.gms:play-services-cast:15.0.1'

    // IMA SDK
    implementation 'com.google.ads.interactivemedia.v3:interactivemedia:3.6.0'
    implementation 'com.google.android.gms:play-services-ads:15.0.1'

    // In-app billing
    implementation 'com.android.billingclient:billing:1.0'

    implementation files('libs/libAkamaiExoPlayerLoader.jar')
    implementation files('libs/libAndroidMediaAnalytics.jar')


    // Fastlane Screengrab
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support:support-annotations:27.1.1'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
    androidTestImplementation 'tools.fastlane:screengrab:1.0.0'
}

// TODO: Uncomment to use In-app Purchases
//apply plugin: 'com.google.gms.google-services'